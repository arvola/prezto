#
# Singleline theme based on the giddie theme by Paul Gideon Dann.
#
# Authors:
#   Paul Gideon Dann <pdgiddie@gmail.com>
#   Mikael Arvola <mikael@arvola.com>
#
# Features:
#   - Simple VCS branch, staged, and unstaged indication.
#   - Last command exit status is displayed when non-zero.
#   - Kubernetes context.
#

function prompt_giddie_precmd {

  # Replace "/home/<user>" with "~"
  _prompt_giddie_pwd="${PWD/#$HOME/~}"

  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_giddie_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook to call our function before each command
  add-zsh-hook precmd prompt_giddie_precmd

  zstyle ':prezto:module:editor:info:completing' format '%F{green}...%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%F{yellow}--- VI-COMMAND ---%f'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'no'
  zstyle ':prezto:module:git:info:action' format ':%F{magenta}%s%f'
  zstyle ':prezto:module:git:info:branch' format '%F{magenta}%b%f'
  zstyle ':prezto:module:git:info:clean' format ' %F{magenta}✔%f'
  zstyle ':prezto:module:git:info:dirty' format ' %F{magenta}✗%f'
  zstyle ':prezto:module:git:info:commit' format '%F{magenta}%.7c%f'
  zstyle ':prezto:module:git:info:position' format '%F{magenta}%p%f'
  zstyle ':prezto:module:git:info:keys' format 'prompt' '%b '

  # Define prompts.
  PROMPT='%(?..%F{red}%B-> [%?]%b%f
)%F{green}%n@%U%m%f%u ${git_info:+${(e)git_info[prompt]}}%F{cyan}${_prompt_giddie_pwd}%f: '

  if (( $+functions[kube_ps1] )); then
    RPROMPT='$(kube_ps1)'
  else
    RPROMPT=''
  fi

  SPROMPT='zsh: correct %F{magenta}%R%f to %F{green}%r%f [nyae]? '
}

prompt_giddie_setup "$@"

